import java.text.SimpleDateFormat

plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.github.johnrengelman.shadow" version '7.0.0'
}

apply plugin: 'maven-publish'

group "io.th0rgal"
def pluginVersion = "1.0.0"
ext.jitpackGroup = group + ".xray"
ext.jitpackVersion = 'main-' + pluginVersion
version = pluginVersion

repositories {
    mavenCentral()
    mavenLocal()
    maven { url 'https://jitpack.io' }
    // Spigot
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    // Because Spigot depends on Bungeecord ChatComponent-API
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    // ProtocolLib
    maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
}

compileJava {
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly group: "org.spigotmc", name: "spigot", version: "1.17-R0.1-SNAPSHOT"
    compileOnly group: "org.spigotmc", name: "spigot-api", version: "1.17-R0.1-SNAPSHOT"
    compileOnly group: "com.github.dmulloy2", name: "ProtocolLib", version: "4.7.0"
    compileOnly group: 'io.netty', name: 'netty-all', version: '4.0.4.Final'
    compileOnly 'com.github.Th0rgal:Oraxen:-SNAPSHOT'
    implementation 'com.github.stefvanschie.inventoryframework:IF:0.10.0'
    implementation 'dev.jorel.CommandAPI:commandapi-shade:6.2.0'
    implementation "net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT"
    implementation "net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT"
}

processResources {
    filesNotMatching(["**/*.png"]) {
        expand projectVersion: pluginVersion
    }
}

shadowJar {
    relocate "com.github.stefvanschie.inventoryframework", "io.th0rgal.xray.shaded.if"
    relocate "dev.jorel.commandapi", "io.th0rgal.xray.shaded.commandapi"
    relocate "net.kyori", "io.th0rgal.oraxen.shaded.kyori"
    manifest {
        attributes(
                'Built-By': System.properties['user.name'],
                'Version': pluginVersion,
                'Build-Timestamp': new SimpleDateFormat("yyyy-MM-dd' 'HH:mm:ss.SSSZ").format(new Date()),
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Build-Jdk': "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})",
                'Build-OS': "${System.properties['os.name']} ${System.properties['os.arch']} ${System.properties['os.version']}"
        )
    }
}

compileJava.dependsOn clean
build.dependsOn shadowJar

publishing {
    publications {
        grp(MavenPublication) {
            from(components.java)
        }
    }
}